buildscript {
    repositories {
        jcenter()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath errorprone
        classpath springDepManager
        classpath springBoot
        classpath testContainersPg
        classpath spotless

    }
}

apply plugin: 'java'
apply plugin: "com.diffplug.spotless"
apply plugin: 'jacoco'
apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'net.ltgt.errorprone'

group = "awsm"
version = "UNSPECIFIED"

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven() {
        url cronapp
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

}

dependencyManagement {
    imports {
        mavenBom springBootBom
    }
}


spotless {
    java {
        target project.fileTree(project.rootDir) {
            include '**/*.java'
            exclude '*/generated-src/**/*.*'
        }
        googleJavaFormat()
    }
}

dependencies {
    errorprone errorproneCore
    errorprone nullaway

    compileOnly lombok
	annotationProcessor lombok

    implementation emojiJava
    implementation commonsText
    implementation jool
    implementation vavr
    implementation commonsLang
    implementation hibernate
    implementation guava
    implementation mug
    implementation caffeine
    implementation streamEx
    implementation faker
    implementation jasypt
    implementation threeten
    implementation hibernateValidator
    implementation simpleJavaMail
    implementation springBootJetty
    implementation springBootJpa
    implementation springTx
    implementation springContext
    implementation springWebMvc
    implementation(springBootWeb) {
        exclude module: "spring-boot-starter-tomcat"
        because 'we use Jetty instead of Tomcat'
    }
    implementation javaxJson
    implementation bucket4j
    implementation failsafe
    implementation pipelinr
    implementation hashIds
    implementation ulidGenerator
    testImplementation wiremock
    testImplementation guavaTestlib
    testImplementation concurrentUnit
    testImplementation groovy
    testImplementation groovyJson
    testImplementation jsonAssert
    testImplementation archUnit
    testImplementation h2
    testImplementation greenmailSpring
    testImplementation spockSpring
    testImplementation springBootTest
    testImplementation springBootTestCfg
	testCompileOnly lombok
	testAnnotationProcessor lombok
}

java {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

jacoco {
    toolVersion = '0.8.7-SNAPSHOT'
}

tasks.withType(Test) {
    maxParallelForks = 4
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "-Xlint:unchecked"
    options.compilerArgs += "-Xlint:preview"
    it.options.errorprone { e ->
        e.disableWarningsInGeneratedCode = true
        e.disable "TypeParameterUnusedInFormals"
        e.disable "StaticAssignmentInConstructor"
        e.option "NullAway:AnnotatedPackages", "awsm"
        e.option "NullAway:ExcludedFieldAnnotations", "org.springframework.beans.factory.annotation.Autowired"
        e.option "NullAway:ExternalInitAnnotations", "javax.persistence.Entity,javax.persistence.MappedSuperclass,javax.persistence.Embeddable"
    }
}

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
}